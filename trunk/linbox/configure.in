AC_INIT(linbox/linbox.dxx)

AM_CONFIG_HEADER(linbox-config.h)
AM_INIT_AUTOMAKE(linbox, 0.1.0)

AM_MAINTAINER_MODE

AM_ACLOCAL_INCLUDE(macros)

AC_PROG_CC
AC_PROG_CXX
AC_STDC_HEADERS
AC_DISABLE_SHARED
AM_PROG_LIBTOOL

# Machine characteristics

AC_CHECK_SIZEOF(char, 8)
AC_CHECK_SIZEOF(short, 16)
AC_CHECK_SIZEOF(int, 32)
AC_CHECK_SIZEOF(long, 32)
AC_CHECK_SIZEOF(long long, 64)
AC_CHECK_SIZEOF(__int64, 64)

# Create some useful data types of fixed, known lengths

case 1 in
  $ac_cv_sizeof_char)
     LINBOX_INT8="char";
     ;;
esac

case 2 in
  $ac_cv_sizeof_short)
     LINBOX_INT16="short";
     ;;
  $ac_cv_sizeof_int)
     LINBOX_INT16="int";
     ;;
esac

case 4 in
  $ac_cv_sizeof_short)
     LINBOX_INT32="short";
     ;;
  $ac_cv_sizeof_int)
     LINBOX_INT32="int";
     ;;
  $ac_cv_sizeof_long)
     LINBOX_INT32="long";
     ;;
esac

case 8 in
  $ac_cv_sizeof_short)
     LINBOX_INT64="short";
     ;;
  $ac_cv_sizeof_int)
     LINBOX_INT64="int";
     ;;
  $ac_cv_sizeof_long)
     LINBOX_INT64="long";
     ;;
  $ac_cv_sizeof_long_long)
     LINBOX_INT64="long long";
     ;;
  $ac_cv_sizeof___int64)
     LINBOX_INT64="__int64";
     ;;
esac

AC_DEFINE_UNQUOTED(__LINBOX_INT8, $LINBOX_INT8, "Canonical 8-bit data type")
AC_DEFINE_UNQUOTED(__LINBOX_INT16, $LINBOX_INT16, "Canonical 16-bit data type")
AC_DEFINE_UNQUOTED(__LINBOX_INT32, $LINBOX_INT32, "Canonical 32-bit data type")
AC_DEFINE_UNQUOTED(__LINBOX_INT64, $LINBOX_INT64, "Canonical 64-bit data type")

# Feature checks

AC_LANG_CPLUSPLUS

LB_CHECK_GMP(,,[
echo '*******************************************************************************'
echo ' ERROR: GMP not found!'
echo
echo ' GMP version 3.1.1 or greater is required for this library to compile. Please'
echo ' make sure GMP is installed and specify its location with the option'
echo ' --with-gmp-prefix=<prefix> when running configure.'
echo '*******************************************************************************'
exit 1
])

LB_CHECK_NTL
LB_CHECK_GIVARO
LB_CHECK_SACLIB
LB_CHECK_LIDIA

AC_CHECK_PROG(DOCPLUSPLUS_SUBDIRS,doc++,"html")

if test "x$DOCPLUSPLUS_SUBDIRS" = "x"; then
   echo '*******************************************************************************'
   echo ' Note: Doc++ not found!'
   echo
   echo ' Documentation will not be built.'
   echo '*******************************************************************************'
   TEX_SUBDIRS=
   AC_SUBST(TEX_SUBDIRS)
else
   AC_CHECK_PROG(TEX_SUBDIRS,latex,"tex")
   if test "x$TEX_SUBDIRS" = "x"; then
      echo '*******************************************************************************'
      echo ' Note: LaTeX not found!'
      echo
      echo ' LaTeX Documentation will not be built.'
      echo '*******************************************************************************'
   fi
fi

CXXFLAGS="${GMP_CFLAGS} ${NTL_CFLAGS} ${GIVARO_CFLAGS} ${SACLIB_CFLAGS} ${CXXFLAGS}"
AC_SUBST(CXXFLAGS)

AC_OUTPUT([
Makefile
linbox-config
doc/Makefile
doc/html/Makefile
doc/tex/Makefile
examples/Makefile
examples/integers/Makefile
examples/blackbox/Makefile
examples/fields/Makefile
gmp++/Makefile
linbox/Makefile
linbox/algorithms/Makefile
linbox/blackbox/Makefile
linbox/element/Makefile
linbox/field/Makefile
linbox/randiter/Makefile
linbox/solutions/Makefile
linbox/util/Makefile
linbox/util/gmp++/Makefile
linbox/vector/Makefile
tests/Makefile
tests/data/Makefile
macros/Makefile
])
